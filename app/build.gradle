apply plugin: 'com.android.application'
apply plugin: 'spoon'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "weatherview.joshskeen.com.weatherview"
        minSdkVersion 14
        // For any flavor using WireMock, Platform 19 is required because for Java 7 which is required by WireMock.
        // This doesn't mean your normal app has to be though
        targetSdkVersion 23
        buildConfigField "String", "WEATHERVIEW_API_KEY", "\"840dbdf2737a7ff9\"";
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Enabling Multidex support for 'androidTest' builds because of large dependencies
        multiDexEnabled = true
    }

    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }

    buildTypes {
        debug {
            buildConfigField "String", "IP", "\"127.0.0.1\""
            buildConfigField "Integer", "PORT", "9998"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
}

dependencies {
    //------ Compile Dependencies ------
    compile('com.squareup.retrofit2:retrofit:2.1.0') {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile "com.squareup.okhttp3:logging-interceptor:3.5.0"
    compile "org.slf4j:slf4j-api:1.7.12"

    //------ Android Test Dependencies ------
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'junit', module: 'junit'
    }
    androidTestCompile 'com.android.support.test:runner:0.5'

    androidTestCompile 'junit:junit:4.12'

    //MockWebServer - Version 2.2 of mockwebserver doesn't work because of an issue, so forcing v2.1 - https://github.com/square/okhttp/issues/1069
    androidTestCompile('com.squareup.okhttp3:mockwebserver:3.5.0') {
        exclude module: 'okhttp'
    }

    //WireMock
    androidTestCompile("com.github.tomakehurst:wiremock:2.5.0") {
        //Using Android Version Instead
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'

        //Version conflict with our app's slf4j version
        exclude group: 'org.slf4j', module: 'slf4j-api'

        //Was getting a classpath conflict for org.objectweb.asm.AnnotationVisitor which is a part of 'net.minidev:asm'
        exclude group: 'org.ow2.asm', module: 'asm'

        //Was getting this warning, so decided to ignore this version included by WireMock.
        //Warning:Dependency org.json:json:20090211 is ignored as it may be conflicting with the internal version provided by Android.
        //In case of problem, please repackage with jarjar to change the class packages
        exclude group: 'org.json', module: 'json'
    }
    androidTestCompile 'org.apache.httpcomponents:httpclient-android:4.3.5.1'
    androidTestCompile 'commons-io:commons-io:2.4'

    androidTestCompile "com.squareup.spoon:spoon-client:1.5.6"
}
configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    all*.exclude group: 'com.android.support', module: 'support-annotations'
}

// This section is optional
spoon {
    // for debug output
    debug = true

    codeCoverage = false

    shard = true
}